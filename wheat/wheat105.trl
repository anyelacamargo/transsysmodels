transsys wg
{
  factor pheophorbide_oxygenase
  {
    decay: 0.5;
    diffusibility: 0.0;
  }

  factor chlorophyll
  {
    decay: 0.5 + pheophorbide_oxygenase;
    diffusibility: 0.0;
  }

  factor senescence
  {
    decay: 0.2;
    diffusibility: 0.0;
  }

  factor globaltime
  {
    decay: 0.0;
    diffusibility: 0.0;
  }

  factor localtime
  {
    decay: 0.0;
    diffusibility: 0.0;
  }

  factor branchlevel
  {
    decay: 0.0;
    diffusibility: 0.0;
  }

  factor vegetative
  {
    decay: 1.0;
    diffusibility: 0.0;
  }


  factor growphytomer
  {
    decay: 0.0;
    diffusibility: 0.0;
  }

  factor growtiller
  {
    decay: 1.0;
    diffusibility: 0.0;
  }

  factor internodelength
  {
    decay: 0.0;
    diffusibility: 0.0;
  }

  gene chlorophyll_gene
  {
    promoter
    {
      constitutive: 0.5 - senescence * 0.5;
    }
    product
    {
      default: chlorophyll;
    }
  }

  gene pheophorbide_oxygenase_gene
  {
    promoter
    {
      senescence: activate(0.05, 0.5);
    }
    product
    {
      default: pheophorbide_oxygenase;
    }
  }

  gene senescence_gene
  {
    promoter
    {
      constitutive: (localtime > 80) * 0.2;
    }
    product
    {
      default: senescence;
    }
  }


  gene vegetativemaster
  {
    promoter
    {
      constitutive: globaltime < 30;
    }
    product
    {
      default: vegetative;
    }
  }

  gene tillermaster
  {
    promoter
    {
      constitutive: (branchlevel < 1) && (globaltime > 3) && (globaltime < 6);
    }
    product
    {
      default: growtiller;
    }
  }

  gene phytomermaster
  {
    promoter
    {
      constitutive: 0.2; # globaltime < 9;
    }
    product
    {
      default: growphytomer;
    }
  }

  gene elongatemaster
  {
    promoter
    {
      constitutive: (vegetative == 0.0) * (4.0 - internodelength) * 0.1;
    }
    product
    {
      default: internodelength;
    }
  }

  gene localticker
  {
    promoter
    {
      constitutive: 1.0;
    }
    product
    {
      default: localtime;
    }
  }

  gene globalticker
  {
    promoter
    {
      constitutive: 1.0;
    }
    product
    {
      default: globaltime;
    }
  }
}


# http://www.usask.ca/agriculture/plantsci/winter_cereals/winter-wheat-production-manual/chapter-10.php
lsys wheat
{
  symbol seed(wg);
  symbol meristem(wg);
  symbol internode(wg);
  symbol mesocotyl(wg);
  symbol blade(wg);
  symbol inflorescenceinternode(wg);
  symbol floret(wg);
  symbol awn(wg);
  symbol branch;
  symbol r;
  symbol grainshell;
  symbol [;
  symbol ];

  axiom seed();

  rule germinate
  {
    seed(t) -->
    grainshell
    mesocotyl(transsys t: growphytomer = 0.0)
    [ blade(transsys t: localtime = 0.0, growphytomer = 0.0) ]
    internode(transsys t: internodelength = 0.1, growphytomer = 0.0)
    r
    meristem(transsys t: growphytomer = 0.0)
  }

  rule makephytomer
  {
    # tillering not modelled currently
    meristem(t) : (t.growphytomer >= 1) && (t.vegetative == 1.0) -->
    [ blade(transsys t: localtime = 0.0, growphytomer = 0.0) ]
    internode(transsys t: localtime = 0.0, growphytomer = 0.0, internodelength = 0.05)
    r
    meristem(transsys t: growphytomer = 0.0)
  }

  rule makefloret
  {
    meristem(t) : (t.growphytomer >= 1) && (t.vegetative < 1.0)  -->
    [ floret(transsys t: localtime = 0.0, growphytomer = 0.0) awn(transsys t: localtime = 0.0, growphytomer = 0.0) ]
    inflorescenceinternode(transsys t: localtime = 0.0, growphytomer = 0.0)
    r
    meristem(transsys t: growphytomer = 0.0)
  }

  graphics
  {
    seed
    {
      color(1.0, 1.0, 0.0);
      sphere(0.4);
    }
    grainshell
    {
      color(0.5, 0.3, 0.1);
      sphere(0.5);
      move(0.5);
    }
    internode
    {
      move(internodelength * 0.5);
      color(1.0 - chlorophyll * 2, 1.0, 0.0);
      cylinder(0.2, internodelength);
      move(internodelength * 0.5);
    }
    inflorescenceinternode
    {
      move(0.05);
      color(0, 0, 1);
      cylinder(0.2, 0.1);
      move(0.05);
    }
    floret
    {
      bank(60);
      move(0.3);
      color(1.0, 0.7, 0.7);
      sphere(0.15);
    }
    awn
    {
      bank(-50);
      color(1, 1, 0);
      move(1.0);
      cylinder(0.1, 2.0);
    }
    mesocotyl
    {
      bank(-90);
      move(0.5);
      color(0.9, 0.9, 0.9);
      cylinder(0.2, 1.0);
      move(0.5);
    }
    blade
    {
      bank(40);
      color(1.0 - chlorophyll * 2, 1.0, 0.0);
      move(2.0);
      box(1.0, 0.05, 4.0);
    }
    meristem
    {
      move(0.1);
      color(1.0, 0.0, 0.0);
      sphere(0.2);
    }
    branch { bank(20); }
    r { roll(180); }
    [ { push(); }
    ] { pop(); }
  }
}
